// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
		// let's start by incrementing out[t]
		Inc16(in=regout, out=incin);

		// start processing from the last else-if statement on up
		// because last else-if is reached only if previous are false
		// and so on up the chain
		Mux16(sel=inc, a=regout, b=incin, out=o1);
		Mux16(sel=load, a=o1, b=in, out=o2);
		Mux16(sel=reset, a=o2, b=false, out=o3);

		// put o3 in the register
		Register(load=true, in=o3, out=regout, out=out);
}
